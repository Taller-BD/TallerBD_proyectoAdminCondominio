/* 0. Errores */
-- Secuencia y tabla de errores
/* DROP SEQUENCE seq_errores_detectados; */
CREATE SEQUENCE seq_errores_detectados START WITH 1 INCREMENT BY 1;

/* DROP TABLE ERRORES_DETECTADOS */
CREATE TABLE ERRORES_DETECTADOS (
    error_id  NUMBER PRIMARY KEY,
    mensaje   VARCHAR2(4000),
    fecha_reg DATE DEFAULT SYSDATE
);

-- Package registrar errores
CREATE OR REPLACE PACKAGE pkg_registro_errores AS
    PROCEDURE sp_registrar_error(p_mensaje VARCHAR2);
END pkg_registro_errores;

-- Body del Package Erorres
CREATE OR REPLACE PACKAGE BODY pkg_registro_errores AS
    PROCEDURE sp_registrar_error(p_mensaje VARCHAR2)
        IS
            BEGIN
                INSERT INTO ERRORES_DETECTADOS(error_id, mensaje)
                VALUES (seq_errores_detectados.NEXTVAL, SUBSTR(NVL(p_mensaje,''),1,4000));
            EXCEPTION
                WHEN OTHERS THEN NULL; 
            END;
END pkg_registro_errores;


/* 1. Tabla de registro de pagos */
CREATE TABLE registro_pagos (
    id_log         NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
    fecha_pago     DATE,
    monto_pagado   NUMBER(12,2),
    nro_depto      NUMBER,
    tipo_persona   VARCHAR2(100)
);

-- Trigger que registra cada pago en la tabla de registro pagos 
CREATE OR REPLACE TRIGGER tgr_registra_pago
AFTER INSERT ON PAGO_GASTO_COMUN
FOR EACH ROW
DECLARE
    v_tipo_persona VARCHAR2(100);
BEGIN
    BEGIN
        -- Obtenemos el tipo de persona desde las tablas relacionadas
        SELECT tp.descripcion_tper
        INTO v_tipo_persona
        FROM GASTO_COMUN gc
        JOIN RESPONSABLE_PAGO_GASTO_COMUN rpgc ON gc.numrun_rpgc = rpgc.numrun_rpgc
        JOIN TIPO_PERSONA tp ON rpgc.id_tper = tp.id_tper
        WHERE gc.id_edif = :NEW.id_edif
        AND gc.nro_depto = :NEW.nro_depto
        AND gc.anno_mes_pcgc = :NEW.anno_mes_pcgc;

    EXCEPTION
        WHEN NO_DATA_FOUND THEN
            v_tipo_persona := 'NO ESPECIFICADO';
        WHEN TOO_MANY_ROWS THEN
            v_tipo_persona := 'MULTIPLES REGISTROS';
    END;

    -- Registramos el pago en la tabla de registro
    INSERT INTO registro_pagos(
        fecha_pago,
        monto_pagado,
        nro_depto,
        tipo_persona
    ) VALUES (
        :NEW.fecha_cancelacion_pgc,             
        :NEW.monto_cancelado_pgc,    
        :NEW.nro_depto,      
        v_tipo_persona       
    );

EXCEPTION
    WHEN OTHERS THEN
        pkg_registro_errores.sp_registrar_error(
            'Error en trigger tgr_registra_pago: ' || SQLERRM ||
            ' para depto: ' || :NEW.nro_depto ||
            ', Edificio: ' || :NEW.id_edif ||
            ', mes: ' || :NEW.anno_mes_pcgc
        );
END;


/* 2. Package con la solución */
CREATE OR REPLACE PACKAGE pkg_admin_condominio AS
    /* Funciones */
    FUNCTION fc_total_gastos_mes(p_id_edif NUMBER, p_anno_mes NUMBER) RETURN NUMBER;
    FUNCTION fc_porcentaje_depto(p_id_edif NUMBER, p_nro_depto NUMBER) RETURN NUMBER;

    /* Procedimientos */
    PROCEDURE sp_prorratea_gastos(
        p_id_edif   NUMBER,
        p_anno_mes  NUMBER,
        p_nro_depto NUMBER DEFAULT NULL
    );

    PROCEDURE sp_registrar_pago(
        p_id_edif    NUMBER,
        p_nro_depto  NUMBER,
        p_monto      NUMBER,
        p_anno_mes   NUMBER
    );
END pkg_admin_condominio;

/* Body del package admin condominio */
CREATE OR REPLACE PACKAGE BODY pkg_admin_condominio AS
    TYPE rec_gasto IS RECORD(
        id_edif     GASTO_COMUN.id_edif%TYPE,
        nro_depto   GASTO_COMUN.nro_depto%TYPE,
        monto_total GASTO_COMUN.monto_total_gc%TYPE
    );

    TYPE varray_montos    IS VARRAY(100) OF NUMBER(12,2);
    TYPE varray_deptos    IS VARRAY(100) OF GASTO_COMUN.nro_depto%TYPE;
    TYPE varray_prorrateo IS VARRAY(100) OF NUMBER(12,2);

    /* Cursores */
    CURSOR cur_gastos(p_id_edif GASTO_COMUN.id_edif%TYPE,
                    p_anno_mes GASTO_COMUN.anno_mes_pcgc%TYPE)
        IS
            SELECT id_edif, nro_depto, monto_total_gc
            FROM GASTO_COMUN
            WHERE id_edif = p_id_edif
            AND anno_mes_pcgc = p_anno_mes;

    CURSOR cur_gastos_filtrado(p_id_edif GASTO_COMUN.id_edif%TYPE,
                             p_anno_mes GASTO_COMUN.anno_mes_pcgc%TYPE,
                             p_nro_depto GASTO_COMUN.nro_depto%TYPE)
        IS
            SELECT id_edif, nro_depto, monto_total_gc
            FROM GASTO_COMUN
            WHERE id_edif = p_id_edif
            AND anno_mes_pcgc = p_anno_mes
            AND (p_nro_depto IS NULL OR nro_depto = p_nro_depto);


    /* Función: sumar total de montos para un edificio/mes */
    FUNCTION fc_total_gastos_mes(p_id_edif NUMBER, p_anno_mes NUMBER)
        RETURN NUMBER
            IS
                v_total NUMBER := 0;
                BEGIN
                    FOR r IN cur_gastos(p_id_edif, p_anno_mes)
                        LOOP
                            v_total := v_total + NVL(r.monto_total_gc,0);
                        END LOOP;
                    RETURN v_total;
                EXCEPTION
                    WHEN OTHERS THEN
                        pkg_registro_errores.sp_registrar_error('Total gastos mes: '||SQLERRM||
                                                                'ID Edificio: '||p_id_edif||', mes: '||p_anno_mes);
                    RETURN 0;
                END;

    /* Función: obtener porcentaje prorrateo de dpto */
    FUNCTION fc_porcentaje_depto(p_id_edif NUMBER, p_nro_depto NUMBER)
        RETURN NUMBER
            IS
                v_por NUMBER := 0;
                BEGIN
                    SELECT PORC_PRORRATEO_DEPTO
                    INTO v_por
                    FROM DEPARTAMENTO
                    WHERE id_edif = p_id_edif
                    AND nro_depto = p_nro_depto;

                    RETURN NVL(v_por,0);
                EXCEPTION
                    WHEN NO_DATA_FOUND THEN
                        pkg_registro_errores.sp_registrar_error(
                            'Porcentaje Dpto: depto no encontrado '||
                            'ID Edificio: '||p_id_edif||', Nro Dpto: '||p_nro_depto
                        );
                        RETURN 0;
                    WHEN TOO_MANY_ROWS THEN
                        pkg_registro_errores.sp_registrar_error(
                            'Porcentaje Dpto: depto duplicado '||
                            'ID Edificio: '||p_id_edif||', Nro Dpto: '||p_nro_depto
                        );
                        RETURN 0;
                    WHEN OTHERS THEN
                        pkg_registro_errores.sp_registrar_error(
                            'Porcentaje Dpto: '||SQLERRM||
                            ' ID Edificio: '||p_id_edif||', Nro Dpto: '||p_nro_depto
                        );
                        RETURN 0;
                END;

    /* Procedimiento */
    PROCEDURE sp_prorratea_gastos(p_id_edif NUMBER, p_anno_mes NUMBER, p_nro_depto NUMBER)
        IS
            v_reg           rec_gasto;
            v_montos        varray_montos := varray_montos(); 
            v_deptos        varray_deptos := varray_deptos();
            v_prorr         varray_prorrateo := varray_prorrateo();
            v_idx           NUMBER := 0; 
            v_total         NUMBER := 0;
            v_suma_pror     NUMBER := 0;
            
            BEGIN
                /* Cursor simple */
                FOR r IN cur_gastos(p_id_edif, p_anno_mes) LOOP
                    IF v_deptos.COUNT = v_deptos.LIMIT OR v_montos.COUNT = v_montos.LIMIT THEN
                        pkg_registro_errores.sp_registrar_error('Límite de VARRAY alcanzado (100)');    
                        EXIT;
                    END IF;

                    v_idx := v_idx + 1;
                    v_deptos.EXTEND; v_deptos(v_idx) := r.nro_depto;
                    v_montos.EXTEND; v_montos(v_idx) := NVL(r.monto_total_gc,0);

                    pkg_registro_errores.sp_registrar_error(
                        'Carga base depto: '||r.nro_depto||', monto: '||NVL(r.monto_total_gc,0));
                END LOOP;

                /* Cursor con parámetros */
                OPEN cur_gastos_filtrado(p_id_edif, p_anno_mes, p_nro_depto);
                    LOOP
                        FETCH cur_gastos_filtrado INTO v_reg;
                        EXIT WHEN cur_gastos_filtrado%NOTFOUND;

                        IF v_reg.monto_total IS NULL THEN
                            pkg_registro_errores.sp_registrar_error(
                                'Monto nulo detectado en gasto común '||
                                ' ID Edificio: '||p_id_edif||', Dpto: '||v_reg.nro_depto||', mes: '||p_anno_mes);
                        END IF;

                        v_total := v_total + v_reg.monto_total;
                    END LOOP;
                CLOSE cur_gastos_filtrado;

                pkg_registro_errores.sp_registrar_error(
                    'Total del período (filtro '||NVL(TO_CHAR(p_nro_depto)||' TODOS')||') = '||v_total||
                    ' ID Edificio: '||p_id_edif||', mes: '||p_anno_mes
                );

                /* Cálculo de prorrateo por porcentaje de DEPARTAMENTO */
                IF v_idx = 0 THEN
                    pkg_registro_errores.sp_registrar_error('No hay deptos para prorratear');
                    RETURN;
                END IF;

                FOR i IN 1..v_idx LOOP
                    IF v_prorr.COUNT = v_prorr.LIMIT THEN
                        pkg_registro_errores.sp_registrar_error('Límite prorrateos VARRAY');
                        EXIT;
                    END IF;

                    DECLARE
                        v_por NUMBER := fc_porcentaje_depto(p_id_edif, v_deptos(i));
                    BEGIN
                        v_prorr.EXTEND;
                        v_prorr(i) := ROUND(v_total * (NVL(v_por,0)/100), 2);
                        v_suma_pror := v_suma_pror + v_prorr(i);

                        pkg_registro_errores.sp_registrar_error(
                            'Depto '||v_deptos(i)||' Porcentaje: '||NVL(v_por,0)||'% Prorrateo = '||v_prorr(i)||', Monto original = '||v_montos(i));
                    END;
                END LOOP;

                pkg_registro_errores.sp_registrar_error('Suma prorrateos: '||v_suma_pror);
            EXCEPTION
                WHEN OTHERS THEN
                    pkg_registro_errores.sp_registrar_error('Prorrateo gastos: '||SQLERRM||
                                                            ' ID Edifciio: '||p_id_edif||', mes: '||p_anno_mes);
            END;

    /* Procedimiento de pagos */
    PROCEDURE sp_registrar_pago(p_id_edif NUMBER, p_nro_depto NUMBER, p_monto NUMBER, p_anno_mes NUMBER)
        IS
            BEGIN
                INSERT INTO PAGO_GASTO_COMUN(id_edif, nro_depto, anno_mes_pcgc, fecha_cancelacion_pgc, monto_cancelado_pgc)
                VALUES (p_id_edif, p_nro_depto, p_anno_mes, SYSDATE, p_monto);
            EXCEPTION
                WHEN OTHERS THEN
                    pkg_registro_errores.sp_registrar_error('Registrar pago: '||SQLERRM||
                    ', ID EDifiucio: '||p_id_edif||
                    ', Depto: '||p_nro_depto||
                    ', Monto: '||p_monto||
                    ', Periodo: '||p_anno_mes);
            END;
END pkg_admin_condominio;